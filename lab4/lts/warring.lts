// Carles Matoses Gimenez
// Ignasi Granell Verdell

const False = 0
const True = 1
range Bool = False..True
set BoolActions = {setTrue, setFalse, [False], [True]}

set CardActions = {set1, set2, [1], [2]}
range Card = 1..2

BOOLVAR = VAL[False],
VAL[v:Bool] = (setTrue -> VAL[True]
            |setFalse -> VAL[False]
            |[v] -> VAL[v]
            ).

CARDVAR = VAL[1],
VAL[i:Card] = (set1 -> VAL[1] | set2 -> VAL[2] | [i] -> VAL[i]).

||FLAGSTURN = (flag1:BOOLVAR || flag2:BOOLVAR || turn:CARDVAR).

property MUTEX = (n1.enter -> n1.exit -> MUTEX 
                  | n2.enter -> n2.exit -> MUTEX).

NEIGHBOR1 = (flag1.setTrue -> turn.set2 -> TEST1),
TEST1 = (flag2[raised:Bool] -> turn[card:Card] ->
		if (raised && card == 2) then TEST1
		else (enter -> exit -> flag1.setFalse -> NEIGHBOR1)
		) + {{flag1, flag2}.BoolActions, turn.CardActions}.

NEIGHBOR2 = (flag2.setTrue -> turn.set1 -> TEST2),
TEST2 = (flag1[raised:Bool] -> turn[card:Card] ->
		if (raised && card == 1) then TEST2
		else (enter -> exit -> flag2.setFalse -> NEIGHBOR2)
		) + {{flag1, flag2}.BoolActions, turn.CardActions}.

||FIELD = (n1:NEIGHBOR1 || n2:NEIGHBOR2 || {n1,n2}::FLAGSTURN || MUTEX).
||GREEDY = FIELD << {n1.flag1.setTrue, n2.flag2.setTrue}.

progress NEIGHBOR1PROG = {n1.enter}
progress NEIGHBOR2PROG = {n2.enter}
